             PGM        PARM(&FILELIB &OBJLIB &DSPPGMREF &FNDFILUSG +
                          &OUTMBR)

             DCLF       FILE(QADSPDBR)

             DCL        VAR(&FILELIB) TYPE(*CHAR) LEN(20)
             DCL        VAR(&OBJLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DSPPGMREF) TYPE(*CHAR) LEN(20)
             DCL        VAR(&FNDFILUSG) TYPE(*CHAR) LEN(20)
             DCL        VAR(&OUTMBR) TYPE(*CHAR) LEN(20)

             DCL        VAR(&FILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&LIBRARY) TYPE(*CHAR) LEN(10)

             DCL        VAR(&PGMREFFILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PGMREFLIB) TYPE(*CHAR) LEN(10)

             DCL        VAR(&FILUSGFILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FILUSGLIB) TYPE(*CHAR) LEN(10)

             DCL        VAR(&MEMBER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MBROPT) TYPE(*CHAR) LEN(8)

             DCL        VAR(&@MSGID) TYPE(*CHAR) LEN(7)

             DLTF       FILE(QTEMP/QAFDBASIP)
             MONMSG     MSGID(CPF0000)

             DLTF       FILE(QTEMP/QADSPDBR)
             MONMSG     MSGID(CPF0000)

             CHGVAR     VAR(&FILE) VALUE(%SST(&FILELIB 1 10))
             CHGVAR     VAR(&LIBRARY) VALUE(%SST(&FILELIB 11 10))

             CHGVAR     VAR(&PGMREFFILE) VALUE(%SST(&DSPPGMREF 1 10))
             CHGVAR     VAR(&PGMREFLIB) VALUE(%SST(&DSPPGMREF 11 10))

             CHGVAR     VAR(&FILUSGFILE) VALUE(%SST(&FNDFILUSG 1 10))
             CHGVAR     VAR(&FILUSGLIB) VALUE(%SST(&FNDFILUSG 11 10))

             CHGVAR     VAR(&MEMBER) VALUE(%SST(&OUTMBR 3 10))
             CHGVAR     VAR(&MBROPT) VALUE(%SST(&OUTMBR 13 8))

/* b01 */    IF         COND(&MEMBER *EQ *FIRST) THEN(DO)
             RTVMBRD    FILE(&FILUSGLIB/&FILUSGFILE) RTNMBR(&MEMBER)
             MONMSG     MSGID(CPF0000)
/* e01 */    ENDDO

/* b01 */    IF         COND(&FILE *NE ' ') THEN(DO)
             DSPFD      FILE(&LIBRARY/&FILE) TYPE(*BASATR) +
                          OUTPUT(*OUTFILE) FILEATR(*PF) +
                          OUTFILE(QTEMP/QAFDBASIP)
             DSPDBR     FILE(&LIBRARY/&FILE) OUTPUT(*OUTFILE) +
                          OUTFILE(QTEMP/QADSPDBR)
/* e01 */    ENDDO

             RMVMSG     CLEAR(*ALL)

             OVRDBF     FILE(QADSPDBR) TOFILE(QTEMP/QADSPDBR)

             RCVF
             MONMSG     MSGID(CPF0000)
             RCVMSG     MSGTYPE(*EXCP) MSGID(&@MSGID)

/* b01 */    DOWHILE    COND(&@MSGID *NE CPF0864)

/* b02 */    IF         COND(&WHTYPE *NE ' ') THEN(DO)
/* b03 */    IF         COND(&WHREFI *NE ' ') THEN(DO)

             DSPFD      FILE(&WHRELI/&WHREFI) TYPE(*BASATR) +
                          OUTPUT(*OUTFILE) FILEATR(*LF) +
                          OUTFILE(QTEMP/QAFDBASIP) OUTMBR(*FIRST *ADD)
/* e03 */    ENDDO
/* e02 */    ENDDO

 AGAIN:      RCVF
             MONMSG     MSGID(CPF0000)
             RCVMSG     MSGTYPE(*EXCP) MSGID(&@MSGID)
/* e01 */    ENDDO

             DLTOVR     FILE(*ALL)

             RMVMSG     CLEAR(*ALL)

             OVRDBF     FILE(QAFDBASI) TOFILE(QTEMP/QAFDBASIP) +
                          SHARE(*YES)
             OPNQRYF    FILE((QTEMP/QAFDBASIP)) OPTION(*ALL) +
                          KEYFLD((ATFILE))

             CALL       PGM(FNDFILUSG1)

             CLOF       OPNID(QAFDBASIP)

             DLTOVR     FILE(*ALL)

             DLTF       FILE(QTEMP/&FILUSGFILE)
             MONMSG     MSGID(CPF0000)

             CRTDUPOBJ  OBJ(&PGMREFFILE) FROMLIB(&PGMREFLIB) +
                          OBJTYPE(*FILE) TOLIB(QTEMP) +
                          NEWOBJ(&FILUSGFILE) CST(*NO) TRG(*NO)
/* b01 */    SELECT
/* b02 */    WHEN       COND(&MEMBER = *FIRST) THEN(DO)
             RNMM       FILE(QTEMP/&FILUSGFILE) MBR(QDSPPGMREF) +
                          NEWMBR(&FILUSGFILE)
             CHGOBJD    OBJ(QTEMP/&FILUSGFILE) OBJTYPE(*FILE) +
                          TEXT('System supplied outfile for FNDFILUSG')
             CHGPFM     FILE(QTEMP/&FILUSGFILE) MBR(&MEMBER) +
                          TEXT('System supplied outfile for FNDFILUSG')
/* e02 */    ENDDO
/* b02 */    WHEN       COND(&MEMBER *EQ *FILE) THEN(DO)
             RNMM       FILE(QTEMP/&FILUSGFILE) MBR(QDSPPGMREF) +
                          NEWMBR(&FILUSGFILE)
             CHGOBJD    OBJ(QTEMP/&FILUSGFILE) OBJTYPE(*FILE) +
                          TEXT('System supplied outfile for FNDFILUSG')
             CHGPFM     FILE(QTEMP/&FILUSGFILE) MBR(&MEMBER) +
                          TEXT('System supplied outfile for FNDFILUSG')
/* e02 */    ENDDO
/* e01 */    ENDSELECT

/* b01 */    IF         COND(&MBROPT = *REPLACE) THEN(DO)
             CLRPFM     FILE(QTEMP/&FILUSGFILE) MBR(&MEMBER)
/* e01 */    ENDDO

             RMVMSG     CLEAR(*ALL)

             OVRDBF     FILE(QAFDBASI) TOFILE(QTEMP/QAFDBASIP) +
                          SHARE(*YES)
             OPNQRYF    FILE((QTEMP/QAFDBASIP)) OPTION(*INP) +
                          KEYFLD((ATFILE))

             OVRDBF     FILE(QADSPPGM) TOFILE(QTEMP/&FILUSGFILE)

             CALL       PGM(FNDFILUSG2) PARM(&OBJLIB)

             CLOF       OPNID(QAFDBASIP)

             DLTOVR     FILE(*ALL)

             OVRDBF     FILE(QADSPPGM) TOFILE(QTEMP/&FILUSGFILE) +
                          SHARE(*YES)
             OPNQRYF    FILE((QTEMP/&FILUSGFILE)) OPTION(*ALL) +
                          KEYFLD((WHPNAM) (WHFNAM))

             CALL       PGM(FNDFILUSG3)

             CLOF       OPNID(&FILUSGFILE)

             DLTOVR     FILE(*ALL)

/* b01 */    IF         COND(&MBROPT = *REPLACE) THEN(DO)
             CPYF       FROMFILE(QTEMP/&FILUSGFILE) +
                          TOFILE(&FILUSGLIB/&FILUSGFILE) +
                          TOMBR(&MEMBER) MBROPT(*REPLACE) CRTFILE(*YES)
             MONMSG     MSGID(CPF0000)
/* e01 */    ENDDO
/* b01 */    ELSE       CMD(DO)
             CPYF       FROMFILE(QTEMP/&FILUSGFILE) +
                          TOFILE(&FILUSGLIB/&FILUSGFILE) +
                          TOMBR(&MEMBER) MBROPT(*ADD) CRTFILE(*YES)
             MONMSG     MSGID(CPF0000)
/* e01 */    ENDDO

             DLTF       FILE(QTEMP/QAFDBASIP)
             MONMSG     MSGID(CPF0000)

             DLTF       FILE(QTEMP/QADSPDBR)
             MONMSG     MSGID(CPF0000)

             DLTF       FILE(QTEMP/&FILUSGFILE)
             MONMSG     MSGID(CPF0000)

             RMVMSG     CLEAR(*ALL)

             ENDPGM
