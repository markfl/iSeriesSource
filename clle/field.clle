             PGM        PARM(&FILE &SRCFILE &MEMBER &RPGTYPE &TYPE +
                          &CSPEC &PREFIXSRC &PREFIXDST &CASE)

             DCL        VAR(&FILE) TYPE(*CHAR) LEN(20)
             DCL        VAR(&SRCFILE) TYPE(*CHAR) LEN(20)
             DCL        VAR(&MEMBER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&RPGTYPE) TYPE(*CHAR) LEN(8)
             DCL        VAR(&TYPE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&CSPEC) TYPE(*CHAR) LEN(7)
             DCL        VAR(&PREFIXSRC) TYPE(*CHAR) LEN(4)
             DCL        VAR(&PREFIXDST) TYPE(*CHAR) LEN(4)
             DCL        VAR(&CASE) TYPE(*CHAR) LEN(6)

             DCL        VAR(&FROMFILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FROMLIB) TYPE(*CHAR) LEN(10)

             DCL        VAR(&AGAIN) TYPE(*LGL)

             INCLUDE    SRCMBR(RCVMSGSRCD) SRCFILE(QCPYSRC)

             CHGVAR     VAR(&FROMFILE) VALUE(%SST(&FILE 1 10))
             CHGVAR     VAR(&FROMLIB) VALUE(%SST(&FILE 11 10))

             DLTF       FILE(QTEMP/QADSPFFD)
             MONMSG     MSGID(CPF0000)

             DLTF       FILE(QTEMP/QAFDACCP)
             MONMSG     MSGID(CPF0000)

             DLTF       FILE(QTEMP/QADSPOBJ)
             MONMSG     MSGID(CPF0000)
             RMVMSG     CLEAR(*ALL)

 AGAIN:      IF         COND(&FROMLIB *EQ '*LIBL') THEN(RTVOBJD +
                          OBJ(&FROMFILE) OBJTYPE(*FILE) +
                          RTNLIB(&FROMLIB))

             DSPFD      FILE(&FROMLIB/&FROMFILE) TYPE(*ACCPTH) +
                          OUTPUT(*OUTFILE) OUTFILE(QTEMP/QAFDACCP)
             DSPFFD     FILE(&FROMLIB/&FROMFILE) OUTPUT(*OUTFILE) +
                          OUTFILE(QTEMP/QADSPFFD)
             DSPOBJD    OBJ(&FROMLIB/&FROMFILE) OBJTYPE(*FILE) +
                          OUTPUT(*OUTFILE) OUTFILE(QTEMP/QADSPOBJ)
             RMVMSG     CLEAR(*ALL)

             OVRDBF     FILE(QADSPFFD) TOFILE(QTEMP/QADSPFFD)
             OVRDBF     FILE(QAFDACCP) TOFILE(QTEMP/QAFDACCP)
             OVRDBF     FILE(QADSPOBJ) TOFILE(QTEMP/QADSPOBJ)

             CALL       PGM(FIELDR) PARM(&FROMFILE &FROMLIB &AGAIN)

             DLTOVR     FILE(*ALL)

 DELETE:     DLTF       FILE(QTEMP/QADSPFFD)
             MONMSG     MSGID(CPF0000)

             DLTF       FILE(QTEMP/QAFDACCP)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(QTEMP/QADSPOBJ)
             MONMSG     MSGID(CPF0000)

             IF         COND(&AGAIN) THEN(DO)
             IF         COND(&FROMLIB *EQ ' ') THEN(CHGVAR +
                          VAR(&FROMLIB) VALUE(*LIBL))
             RMVMSG     CLEAR(*ALL)
             GOTO       CMDLBL(AGAIN)
             ENDDO

             INCLUDE    SRCMBR(RCVMSGSRC) SRCFILE(QCPYSRC)

             ENDPGM
