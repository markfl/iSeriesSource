             PGM        PARM(&FRMFILELIB &TOFILELIB &TOMBRS +
                          &FROMPREFIX &EXTENSION)

             DCLF       FILE(QAFDMBRL)

             DCL        VAR(&FRMFILELIB) TYPE(*CHAR) LEN(20)
             DCL        VAR(&TOFILELIB) TYPE(*CHAR) LEN(20)
             DCL        VAR(&TOMBRS) TYPE(*CHAR) LEN(20)
             DCL        VAR(&FROMPREFIX) TYPE(*CHAR) LEN(15)
             DCL        VAR(&EXTENSION) TYPE(*CHAR) LEN(10)

             DCL        VAR(&FRMFILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FRMLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TOFILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TOLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TOMBR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MBROPT) TYPE(*CHAR) LEN(8)
             DCL        VAR(&NBRCURRCD) TYPE(*DEC) LEN(10 0)

             INCLUDE    SRCMBR(RCVMSGSRCD) SRCFILE(QCPYSRC)

             CHGVAR     VAR(&FRMFILE) VALUE(%SST(&FRMFILELIB 1 10))
             CHGVAR     VAR(&FRMLIB) VALUE(%SST(&FRMFILELIB 11 10))
/* b01 */    IF         COND(&FRMLIB *EQ *CURLIB) THEN(DO)
             CHGVAR     VAR(&FRMLIB) VALUE(&CURLIB)
/* e01 */    ENDDO

             CHGVAR     VAR(&TOFILE) VALUE(%SST(&TOFILELIB 1 10))
             CHGVAR     VAR(&TOLIB) VALUE(%SST(&TOFILELIB 11 10))
/* b01 */    IF         COND(&TOLIB *EQ *CURLIB) THEN(DO)
             CHGVAR     VAR(&TOLIB) VALUE(&CURLIB)
/* e01 */    ENDDO

             CHGVAR     VAR(&TOMBR) VALUE(%SST(&TOMBRS 3 10))
             CHGVAR     VAR(&MBROPT) VALUE(%SST(&TOMBRS 13 8))

/* b01 */    IF         COND(&FROMPREFIX = *FRMSRCFILE) THEN(DO)
             CHGVAR     VAR(&FROMPREFIX) VALUE(&FRMFILE)
/* e01 */    ENDDO

             DLTF       FILE(QTEMP/QAFDMBRL)
             MONMSG     MSGID(CPF0000)

             DSPFD      FILE(&FRMLIB/&FRMFILE) TYPE(*MBRLIST) +
                          OUTPUT(*OUTFILE) OUTFILE(QTEMP/QAFDMBRL)

             ADDPFM     FILE(&TOLIB/&TOFILE) MBR(&TOMBR) SRCTYPE(BAT)
             MONMSG     MSGID(CPF0000)
/* b02 */    IF         COND(&MBROPT *EQ *REPLACE) THEN(DO)
             CLRPFM     FILE(&TOLIB/&TOFILE) MBR(&TOMBR)
             MONMSG     MSGID(CPF0000)
/* e02 */    ENDDO

             OVRDBF     FILE(QAFDMBRL) TOFILE(QTEMP/QAFDMBRL)
             OVRDBF     FILE(QTXTSRC) TOFILE(&TOLIB/&TOFILE) +
                          MBR(&TOMBR)

             CALL       PGM(CRTRNMBCHR) PARM(&FROMPREFIX &EXTENSION)

             DLTOVR     FILE(*ALL)

             RTVMBRD    FILE(&TOLIB/&TOFILE) MBR(&TOMBR) +
                          NBRCURRCD(&NBRCURRCD)

             ZERSRC     FILE(&TOLIB/&TOFILE) MEMBER(&TOMBR)

             SELECT
             WHEN       COND(&NBRCURRCD *LT 10000) THEN(DO)
             RGZPFM     FILE(&TOLIB/&TOFILE) MBR(&TOMBR) +
                          SRCOPT(*SEQNBR)
             MONMSG     MSGID(CPF0000)
             ENDDO
             WHEN       COND(&NBRCURRCD *LT 110000) THEN(DO)
             RGZPFM     FILE(&TOLIB/&TOFILE) MBR(&TOMBR) +
                          SRCOPT(*SEQNBR) SRCSEQ(1.00 0.10)
             MONMSG     MSGID(CPF0000)
             ENDDO
             OTHERWISE  CMD(DO)
             RGZPFM     FILE(&TOLIB/&TOFILE) MBR(&TOMBR) +
                          SRCOPT(*SEQNBR) SRCSEQ(1.00 0.01)
             MONMSG     MSGID(CPF0000)
             ENDDO
             ENDSELECT

             DLTF       FILE(QTEMP/QAFDMBRL)
             MONMSG     MSGID(CPF0000)

             INCLUDE    SRCMBR(RCVMSGSRC) SRCFILE(QCPYSRC)

             ENDPGM
