             PGM        PARM(&FILE &SRCFILE &MEMBER &BYREF)

             DCLPRCOPT  DFTACTGRP(*NO) ACTGRP(*NEW) BNDDIR(MFLORES)

             DCL        VAR(&FILE) TYPE(*CHAR) LEN(20)
             DCL        VAR(&SRCFILE) TYPE(*CHAR) LEN(20)
             DCL        VAR(&MEMBER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&BYREF) TYPE(*LGL)

             DCL        VAR(&FROMFILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FROMLIB) TYPE(*CHAR) LEN(10)

             DCL        VAR(&TOSRCFILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TOSRCLIB) TYPE(*CHAR) LEN(10)

             DCL        VAR(&OBJATR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOIN) TYPE(*LGL)
             DCL        VAR(&UNIQUE) TYPE(*LGL)
             DCL        VAR(&TEXT) TYPE(*CHAR) LEN(50)

             INCLUDE    SRCMBR(RCVMSGSRCD) SRCFILE(QCPYSRC)

             CHGVAR     VAR(&FROMFILE) VALUE(%SST(&FILE 1 10))
             CHGVAR     VAR(&FROMLIB) VALUE(%SST(&FILE 11 10))

             CHGVAR     VAR(&TOSRCFILE) VALUE(%SST(&SRCFILE 1 10))
             CHGVAR     VAR(&TOSRCLIB) VALUE(%SST(&SRCFILE 11 10))

             IF         COND(&MEMBER = *FILE) THEN(CHGVAR +
                          VAR(&MEMBER) VALUE(&FROMFILE))

             DLTF       FILE(QTEMP/QADSPFFD)
             MONMSG     MSGID(CPF0000)

             DLTF       FILE(QTEMP/QAFDACCP)
             MONMSG     MSGID(CPF0000)

             DLTF       FILE(QTEMP/QAFDJOIN)
             MONMSG     MSGID(CPF0000)

             DLTF       FILE(QTEMP/QAFDSELO)
             MONMSG     MSGID(CPF0000)

             DLTF       FILE(QTEMP/QAFDRFMT)
             MONMSG     MSGID(CPF0000)

             RTVOBJD    OBJ(&FROMLIB/&FROMFILE) OBJTYPE(*FILE) +
                          OBJATR(&OBJATR) TEXT(&TEXT)
             DSPFFD     FILE(&FROMLIB/&FROMFILE) OUTPUT(*OUTFILE) +
                          OUTFILE(QTEMP/QADSPFFD)

/* b01 */    IF         COND(&OBJATR *NE DSPF) THEN(DO)

             DSPFD      FILE(&FROMLIB/&FROMFILE) TYPE(*ACCPTH) +
                          OUTPUT(*OUTFILE) OUTFILE(QTEMP/QAFDACCP)

/* e01 */    ENDDO

/* b01 */    IF         COND(&OBJATR *EQ PF) THEN(DO)

             ADDPFM     FILE(&TOSRCLIB/&TOSRCFILE) MBR(&MEMBER) +
                          TEXT(&TEXT) SRCTYPE(PF)
             MONMSG     MSGID(CPF0000)

/* e01 */    ENDDO

/* b01 */    IF         COND(&OBJATR *EQ LF) THEN(DO)

             DSPFD      FILE(&FROMLIB/&FROMFILE) TYPE(*JOIN) +
                          OUTPUT(*OUTFILE) OUTFILE(QTEMP/QAFDJOIN)

             DSPFD      FILE(&FROMLIB/&FROMFILE) TYPE(*SELECT) +
                          OUTPUT(*OUTFILE) OUTFILE(QTEMP/QAFDSELO)

             ADDPFM     FILE(&TOSRCLIB/&TOSRCFILE) MBR(&MEMBER) +
                          TEXT(&TEXT) SRCTYPE(LF)
             MONMSG     MSGID(CPF0000)

/* e01 */    ENDDO

/* b01 */    IF         COND(&OBJATR *EQ DSPF) THEN(DO)

             DSPFD      FILE(&FROMLIB/&FROMFILE) TYPE(*RCDFMT) +
                          OUTPUT(*OUTFILE) OUTFILE(QTEMP/QAFDRFMT)

             ADDPFM     FILE(&TOSRCLIB/&TOSRCFILE) MBR(&MEMBER) +
                          TEXT(&TEXT) SRCTYPE(DSPF)
             MONMSG     MSGID(CPF0000)

/* e01 */    ENDDO

             RMVMSG     CLEAR(*ALL)

             OVRDBF     FILE(QADSPFFD) TOFILE(QTEMP/QADSPFFD)
             OVRDBF     FILE(QDDSSRC) TOFILE(&TOSRCLIB/&TOSRCFILE) +
                          MBR(&MEMBER)

/* b01 */    IF         COND(&OBJATR *NE DSPF) THEN(DO)

             OVRDBF     FILE(QAFDACCP) TOFILE(QTEMP/QAFDACCP)

             CALLPRC    PRC(CHECKFORUNIQUE) RTNVAL(&UNIQUE)

/* e01 */    ENDDO

/* b01 */    IF         COND(&OBJATR *EQ LF) THEN(DO)

             OVRDBF     FILE(QAFDJOIN) TOFILE(QTEMP/QAFDJOIN)

             CALLPRC    PRC(CHECKFORJOIN) RTNVAL(&JOIN)

             OVRDBF     FILE(QAFDSELO) TOFILE(QTEMP/QAFDSELO)

/* e01 */    ENDDO

/* b01 */    IF         COND(&OBJATR *EQ DSPF) THEN(DO)

             OVRDBF     FILE(QAFDRFMT) TOFILE(QTEMP/QAFDRFMT)

/* e01 */    ENDDO

             CALLPRC    PRC(RTVDDSSOURCE) PARM((&OBJATR) (&BYREF) +
                          (&JOIN) (&UNIQUE))
/* b01 */    MONMSG     MSGID(CPF0000) EXEC(DO)
             CLOF       OPNID(QADSPFFD)
             CLOF       OPNID(QAFDACCP)
             CLOF       OPNID(QAFDRFMT)
             CLOF       OPNID(QRPGLESRC)
             RMVM       FILE(&TOSRCLIB/&TOSRCFILE) MBR(&MEMBER)
/* e01 */    ENDDO

             DLTOVR     FILE(*ALL)

             RGZPFM     FILE(&TOSRCLIB/&TOSRCFILE) MBR(&MEMBER) +
                          SRCOPT(*SEQNBR)
             MONMSG     MSGID(CPF0000)

             DLTF       FILE(QTEMP/QADSPFFD)
             MONMSG     MSGID(CPF0000)

             DLTF       FILE(QTEMP/QAFDACCP)
             MONMSG     MSGID(CPF0000)

             DLTF       FILE(QTEMP/QAFDJOIN)
             MONMSG     MSGID(CPF0000)

             DLTF       FILE(QTEMP/QAFDSELO)
             MONMSG     MSGID(CPF0000)

             DLTF       FILE(QTEMP/QAFDRFMT)
             MONMSG     MSGID(CPF0000)

             INCLUDE    SRCMBR(RCVMSGSRC) SRCFILE(QCPYSRC)

 END:        ENDPGM
